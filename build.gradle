plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'net.neoforged.moddev' version '2.0.95'
}

neoForge.version = "21.6.5-beta"
version = '1.5.0'
group = 'dev.gigaherz.playertemplate'
base.archivesName = "PlayerTemplate-${neoForge.minecraftVersion}"

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

java.withSourcesJar()

neoForge {

    runs {
        // applies to all the run configs below
        configureEach {
            gameDirectory = project.file('run')
            logLevel = org.slf4j.event.Level.DEBUG
        }

        client {
            client()
        }

        server {
            server()
        }
    }

    mods {
        thismod {
            sourceSet(sourceSets.main)
        }
    }

    parchment {
        minecraftVersion = "1.21.5"
        mappingsVersion = "2025.06.15"
    }
}

jar {
    from(files('LICENSE.txt'))

    manifest {
        attributes([
                "Specification-Title"     : "playertemplate",
                "Specification-Vendor"    : "gigaherz",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : "playertemplate",
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "gigaherz",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.named('sourcesJar') {
    from(files('LICENSE.txt'))
}

project.afterEvaluate {
    publish.dependsOn('build')
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId project.archivesBaseName
                from components.java
            }
        }

        repositories {
            if (findProperty("RELEASE") && System.env.giga_maven_host != null) {
                System.out.println("Remote publish enabled on " + System.env.giga_maven_host)
                maven {
                    url System.env.giga_maven_host
                    credentials {
                        username System.env.giga_maven_user
                        password System.env.giga_maven_password
                    }
                }
            }
            else {
                System.out.println("Remote publish disabled.")
                maven {
                    url "$projectDir/../CommonMaven"
                }
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
